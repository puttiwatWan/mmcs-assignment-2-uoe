model RefuellingStation
  uses "mmxprs"
  uses "mmsystem"  !! mmsystem is used to create a new folder for storing csv files

  declarations
    nbCustomers: integer  !! number of refuelling stations
    nbSupply: integer     !! number of centralised facilities
  end-declarations

  initialisations from "Dataset1_N25.txt"
	  nbCustomers nbSupply
  end-initialisations

  declarations
    customers = 1..nbCustomers  !! a range of refuelling stations
    supply = 1..nbSupply        !! a range of centralised facilities
    locFacType: set of string   !! types of localized facilities
    locationPoint = 1..2

    !! SiteLocation is a location in latitude and longigude of each refuelling station (used for printing to csv)
    SiteLocation: array(customers, locationPoint) of real
    !! SupplyLocation is a location in latitude and longigude of each centralized facility (used for printing to csv)
    SupplyLocation: array(supply, locationPoint) of real

    !! Demand is the demand for each refuelling station.
    Demand: array(customers) of real
    !! DistCustomerSupply is the distance between each refuelling station and each centralized facility.
    DistCustomerSupply: array(customers, supply) of real
    !! DistCustomer is the distance between each refuelling station.
    DistCustomer: array(customers, customers) of real

    !! LocProdLimit is the production limit for each type of localized facilities.
    LocProdLimit: array(locFacType) of real
    !! MinLocProd is the minimum production required if the localized facility is open.
    MinLocProd: array(locFacType) of real
    !! CenProdLimit is the production limit for each centralized facility.
    CenProdLimit: real
    !! MaxTubePerSite is the mamimum number of tubes that can be used to transport hydrogen to each refuelling station.
    MaxTubePerSite: integer
    !! TubeCap is the capacity of each tube.
    TubeCap: real

    !! CenOpenCost is a cost for opening each centralized facility.
    CenOpenCost: real
    !! TubeTransCost is a cost per km for transporting each tube.
    TubeTransCost: real
    !! EmptyTubeCostReduction is a reduction in cost when transporting an empty tube.
    EmptyTubeCostReduction: real
    !! CenProdCost is a cost per kg when producing hydrogen at a centralized facility.
    CenProdCost: real
    !! LocOpenCost is a cost for opening each type of localized facilities.
    LocOpenCost: array(locFacType) of real
    !! LocProdCost is a cost per kg when producing hydrogen at a localized facility.
    LocProdCost: real
    !! CusOpenCost is a cost for opening each refuelling station.
    CusOpenCost: real
    !! CoveringRange is a range that all stations within can cover the demand for each other
    CoveringRange: real

    !! canCover tells if the first refuelling station can cover the second refuelling station (1 = can cover, 0 otherwise).
    !! e.g. canCover(c1,c2) = 1 means that refuelling station c1 can cover for refuelling station c2.
    canCover: array(customers, customers) of integer

    !! cover is whether the first refuelling station is covering the second refuelling station (1 = cover, 0 otherwise).
    !! e.g. cover(c1,c2) = 1 means that refuelling station c1 is covering for refuelling station c2.
    cover: array(customers, customers) of mpvar

    !! station_open tells which stations are open (1 = open, 0 = closed).
    station_open: array(customers) of mpvar

    !! cen_fac_open tells whether each centralized facility is open (1 = open, 0 = closed).
    cen_fac_open: array(supply) of mpvar
    !! loc_fac_open_at tells whether that refuelling station is used as a localized facility.
    !! e.g. loc_fac_open_at(c1, "L") = 1 means that the large localized facility is open at the
    !! refuelling station c1.
    loc_fac_open_at: array(customers, locFacType) of mpvar

    !! cen_prod tells the amount of hydrogen that is transported from each centralized facility
    !! to each refuelling station.
    cen_prod: array(customers, supply) of mpvar
    !! n_tubes is a number of tubes needed to use to transport from each centralized facility to each station
    n_tubes: array(customers, supply) of mpvar
    !! loc_prod tells the amount of hydrogen that is produced at the refuelling station.
    loc_prod: array(customers, locFacType) of mpvar
  end-declarations

  initialisations from "Dataset1_N25.txt"
    Demand DistCustomerSupply DistCustomer locFacType LocProdLimit MinLocProd CenProdLimit
    MaxTubePerSite TubeCap CenOpenCost TubeTransCost EmptyTubeCostReduction CenProdCost
    LocOpenCost LocProdCost CusOpenCost SiteLocation SupplyLocation CoveringRange
  end-initialisations

  !! Calculate the cover range for each refuelling station
  forall(c1 in customers, c2 in customers) do
    if(DistCustomer(c1,c2) <= CoveringRange) then
      canCover(c1,c2) := 1
    else
      canCover(c1,c2) := 0
    end-if
  end-do


  !! ==================== Objective function ====================
  !! The objective function is to minimized the cost. The total cost can be calculated as:
  !! total_cost = cost for opening each cenralized facility +
  !!              cost for opening each localized facility +
  !!              cost for opening each refuelling station +
  !!              cost for producing each kg at centralized facilities +
  !!              cost for producing each kg at localized facilities +
  !!              cost for tubes transportation back and forth
  total_cost := sum(s in supply) (cen_fac_open(s) * CenOpenCost) +
                sum(c in customers, t in locFacType) (loc_fac_open_at(c,t) * LocOpenCost(t)) +
                sum(c in customers) (station_open(c) * CusOpenCost) +
                sum(c in customers, s in supply) (cen_prod(c,s) * CenProdCost) +
                sum(c in customers, t in locFacType) (loc_prod(c,t) * LocProdCost) +
                sum(c in customers, s in supply) (n_tubes(c,s) * DistCustomerSupply(c,s) * (2 - EmptyTubeCostReduction) * TubeTransCost)
  !! Noted that the (2 - EmptyTubeCostReduction) is for calculating back and forth. When delivery, the cost is just TubeTransCost.
  !! When coming back (with an empty tube), the cost is (1 - EmptyTubeCostReduction) * TubeTransCost. Therefore, combining both,
  !! TubeTransCost + (1 - EmptyTubeCostReduction) * TubeTransCost = (2 - EmptyTubeCostReduction) * TubeTransCost


  !! ==================== Constraints ====================
  !! Each of these variables should be binary
  forall(c in customers) station_open(c) is_binary
  forall(s in supply) cen_fac_open(s) is_binary
  forall(c1 in customers, c2 in customers) cover(c1,c2) is_binary
  forall(c in customers, t in locFacType) loc_fac_open_at(c,t) is_binary

  !! Number of tubes used needs to be an integer
  forall(c in customers, s in supply) n_tubes(c,s) is_integer


  !! --------- Facility Constraints ---------
  !! Each opened refuelling station can only open either a large or small localized facility if needed.
  forall(c in customers) sum(t in locFacType) loc_fac_open_at(c,t) <= station_open(c)


  !! --------- Production Limit Constraints ---------
  !! This is a production limit on how much each opened centralized facility can produce.
  forall(s in supply) sum(c in customers) cen_prod(c,s) <= cen_fac_open(s) * CenProdLimit

  !! Each localized facility has a minimum and maximum production limit for each type.
  !! loc_fac_open_at is used to apply the constraint only if the localized facility is open
  !! at the refuelling station c.
  forall(c in customers, t in locFacType) do
    loc_prod(c,t) <= loc_fac_open_at(c,t) * LocProdLimit(t)
    loc_prod(c,t) >= loc_fac_open_at(c,t) * MinLocProd(t)
  end-do


  !! ---------Transportation Constraints ---------
  !! The number of tubes used for each station needs to be a total production in each centralized facility
  !! rounded up to the nearest multiple of TubeCap and then divided by the TubeCap.
  !! E.g. Let the TubeCap be 1000. If the demand is 4600, n_tubes should be 5.
  forall(c in customers, s in supply) n_tubes(c,s) * TubeCap >= cen_prod(c,s)

  !! The number of tubes used for each station cannot exceed the maximum limit.
  !! E.g. If MaxTubePerSite = 7, each station can only be delivered by no more than 7 tubes.
  forall(c in customers) sum(s in supply) n_tubes(c,s) <= MaxTubePerSite


  !! --------- Station Constraints ---------
  !! Each refuelling station can be covered only once by a capable refuelling station.
  forall(c2 in customers) sum(c1 in customers) cover(c1,c2) * canCover(c1,c2) = 1

  !! Each station can be covered only by an open station
  forall(c1 in customers, c2 in customers) cover(c1,c2) <= station_open(c1)

  !! The total demand of each refuelling station (own demand + covered stations' demand) should not exceed the
  !! total amount of the production from both centralized and localized facilities that can be transported to
  !! that refuelling station.
  forall(c1 in customers)
    (sum(c2 in customers)(Demand(c2) * cover(c1,c2))) <= (sum(s in supply) cen_prod(c1,s)) + (sum(t in locFacType) loc_prod(c1, t))


  !! ==================== Optimize ====================
  writeln("Begin running model")
  minimize(total_cost)
  writeln("End running model")


  !! ==================== Forwardig Procedures ====================
  forward procedure PrintTitle(title:string)
  forward procedure PrintResults(title:string, header:array(range) of string, row_prefix:string, first_iter:range, sec_iter:range, objToPrint:array(range, range) of mpvar, print_int:boolean)
  forward procedure PrintResults(title:string, header:array(range) of string, row_prefix:string, first_iter:range, sec_iter:set of string, objToPrint:array(range, set of string) of mpvar, print_int:boolean)
  forward procedure WriteResultsToCSV(header:string, filename:string, row_prefix:string, first_iter:range, sec_iter:range, objToReadFrom:array(range, range) of mpvar, print_int:boolean)
  forward procedure WriteResultsToCSV(header:string, filename:string, row_prefix:string, first_iter:range, sec_iter:set of string, objToReadFrom:array(range, set of string) of mpvar, print_int:boolean)
  forward procedure WriteDataToCSV(header:string, filename:string, row_prefix:string, first_iter:range, sec_iter:range, objToReadFrom:array(range, range) of real)
  forward procedure WriteDataToCSV(header:string, filename:string, row_prefix:string, first_iter:range, sec_iter:range, objToReadFrom:array(range, range) of integer)


  !! ==================== Printing Results ====================
  !! Crate new folders to store csv files
  results_dir := "results/"
  data_dir := "data/"
  makedir(results_dir)
  makedir(data_dir)

  writeln("==================== Printing Results ====================")
  writeln
  
  !! Calculate each cost
  cen_open_cost := sum(s in supply) (getsol(cen_fac_open(s)) * CenOpenCost)
  l_loc_open_cost := sum(c in customers) (getsol(loc_fac_open_at(c,"L")) * LocOpenCost("L"))
  s_loc_open_cost := sum(c in customers) (getsol(loc_fac_open_at(c,"S")) * LocOpenCost("S"))
  cus_open_cost := sum(c in customers) (getsol(station_open(c)) * CusOpenCost)
  cen_prod_cost := sum(c in customers, s in supply) (getsol(cen_prod(c,s)) * CenProdCost)
  loc_prod_cost := sum(c in customers, t in locFacType) (getsol(loc_prod(c,t)) * LocProdCost)
  trans_cost := sum(c in customers, s in supply) (getsol(n_tubes(c,s)) * DistCustomerSupply(c,s) * (2 - EmptyTubeCostReduction) * TubeTransCost)

  !! Print each cost
  h1 := "Refueling Station Opening Cost"
  h2 := "Centralized Facility Opening Cost"
  h3 := "Large Localized Facility Opening Cost"
  h4 := "Small Localized Facility Opening Cost"
  h5 := "Centralized Facility Production Cost"
  h6 := "Localized Facility Production Cost"
  h7 := "Transportation Cost"
  h8 := "Total Cost"

  PrintTitle("======== Total Cost from Each Source ========")
  writeln(strfmt("Source", 38), " | ", strfmt("Cost (Â£)", 10), " | ")
  forall(i in 1..54) write("-")
  writeln
  writeln(strfmt(h1, 38), " | ", strfmt(cus_open_cost, 10, 2), " | ")
  writeln(strfmt(h2, 38), " | ", strfmt(cen_open_cost, 10, 2), " | ")
  writeln(strfmt(h3, 38), " | ", strfmt(l_loc_open_cost, 10, 2), " | ")
  writeln(strfmt(h4, 38), " | ", strfmt(s_loc_open_cost, 10, 2), " | ")
  writeln(strfmt(h5, 38), " | ", strfmt(cen_prod_cost, 10, 2), " | ")
  writeln(strfmt(h6, 38), " | ", strfmt(loc_prod_cost, 10, 2), " | ")
  writeln(strfmt(h7, 38), " | ", strfmt(trans_cost, 10, 2), " | ")
  forall(i in 1..54) write("-")
  writeln
  writeln(strfmt(h8, 38), " | ", strfmt(getobjval, 10, 2), " | ")
  forall(i in 1..54) write("-")
  writeln

  !! Write each cost to a csv file
  fopen(results_dir + "cost.csv", F_OUTPUT)
    writeln("Source,Cost")
    writeln(h1, ",", strfmt(cus_open_cost, 0, 2))
    writeln(h2, ",", strfmt(cen_open_cost, 0, 2))
    writeln(h3, ",", strfmt(l_loc_open_cost, 0, 2))
    writeln(h4, ",", strfmt(s_loc_open_cost, 0, 2))
    writeln(h5, ",", strfmt(cen_prod_cost, 0, 2))
    writeln(h6, ",", strfmt(loc_prod_cost, 0, 2))
    writeln(h7, ",", strfmt(trans_cost, 0, 2))
    writeln(h8, ",", strfmt(getobjval, 0, 2))
  fclose(F_OUTPUT)

  declarations
    headers3: array(1..3) of string
    headers4: array(1..4) of string
  end-declarations

  headers4(1) := "Customer"
  headers4(2) := "Cen1 (kg)"
  headers4(3) := "Cen2 (kg)"
  headers4(4) := "Cen3 (kg)"

  !! Print total production sending from centralized facilities to each refuelling station
  PrintResults("Total production from centralized facilities to each refuelling station",
                headers4, "c", customers, supply, cen_prod, false)

  headers3(1) := "Customer"
  headers3(2) := "Large Loc (kg)"
  headers3(3) := "Small Loc (kg)"

  !! Print total production in each localized facilities
  PrintResults("Total production in localized facilities",
                headers3, "c", customers, locFacType, loc_prod, false)

  !! Print which refuelling station needs to open a localized facility
  writeln
  PrintTitle("Localized Facility Opened At")
  forall(t in locFacType) do
    forall(c in customers) do
      if (getsol(loc_fac_open_at(c,t)) <> 0) then
          type := "Small"
        if(t = "L") then
          type := "Large"
        end-if
        writeln(strfmt("C" + c, 3), " opens ", type, " localized facility.")
      end-if
    end-do
    writeln
  end-do

  !! Print a number of tubes used to transport hydrogen from each centralized facility to each station
  PrintResults("Number of tubes used from each facility to each refuelling station",
                headers4, "c", customers, supply, n_tubes, true)


  !! =================== Writing results to a csv format ===================
  customer_header := "Customer,"
  forall(c in customers) customer_header := customer_header + "c" + c + ","

  !! Total production in each centralized facility
  fopen(results_dir + "total_cen_prod.csv", F_OUTPUT)
    writeln("Cen1,Cen2,Cen3")
    forall(s in supply) do
      write(strfmt(sum(c in customers) getsol(cen_prod(c,s)), 0, 6), ",")
    end-do
  fclose(F_OUTPUT)

  WriteResultsToCSV(customer_header, "cover.csv", "c", customers, customers, cover, true)
  WriteResultsToCSV("Customer,Cen1,Cen2,Cen3", "cen_prod.csv", "c", customers, supply, cen_prod, false)
  WriteResultsToCSV("Customer,LocLarge,LocSmall", "loc_prod.csv", "c", customers, locFacType, loc_prod, false)
  WriteResultsToCSV("Customer,LocLarge,LocSmall", "loc_open_at.csv", "c", customers, locFacType, loc_fac_open_at, true)


  !! =================== Writing given data to a csv format ===================
  WriteDataToCSV("Customer,Cen1,Cen2,Cen3", "cust_supply_dist.csv", "c", customers, supply, DistCustomerSupply)
  WriteDataToCSV(customer_header, "cust_dist.csv", "c", customers, customers, DistCustomer)
  WriteDataToCSV("Customer,X,Y", "site_location.csv", "c", customers, locationPoint, SiteLocation)
  WriteDataToCSV("Supply,X,Y", "supply_location.csv", "Cen", supply, locationPoint, SupplyLocation)
  WriteDataToCSV(customer_header, "can_cover.csv", "c", customers, customers, canCover)


  !! =================== Procedures ===================
  procedure PrintTitle(title:string)
    banner := "====="
    enclosed := ""
    forall(i in 1..getsize(title)) enclosed += "="
    enclosed += banner + banner + "=="

    writeln(enclosed)  !! print first line
    writeln(banner, " ", title, " ", banner)  !! print title
    writeln(enclosed)  !! print last line
  end-procedure

  procedure PrintResults(title:string, header:array(range) of string, row_prefix:string, first_iter:range, sec_iter:range,
                          objToPrint:array(range, range) of mpvar, print_int:boolean)
    declarations
      sep = " | "
      col_length: array(1..getsize(header)) of integer
    end-declarations

    line_length := 0
    decimal_points := 6
    if(print_int) then
      decimal_points := 0
    end-if

    max_col_length := 12
    if(print_int) then
      max_col_length := 2
    end-if

    writeln
    PrintTitle(title)
    !! print table header
    forall(i in 1..getsize(header)) do
      space := maxlist(getsize(header(i)), max_col_length)
      write(strfmt(header(i), space), sep)
      line_length += space + getsize(sep)
      col_length(i) := space
    end-do
    writeln

    !! print line sep
    forall(i in 1..line_length) write("-")
    writeln

    !! print data
    forall(i in first_iter) do
      write(strfmt(row_prefix + i, col_length(1)), sep)
      forall(j in sec_iter) do
        write(strfmt(abs(getsol(objToPrint(i,j))), col_length(j+1), decimal_points), sep)
      end-do
      writeln
    end-do

    !! print endling line
    forall(i in 1..line_length) write("-")
    writeln
  end-procedure

  procedure PrintResults(title:string, header:array(range) of string, row_prefix:string, first_iter:range, sec_iter:set of string,
                          objToPrint:array(range, set of string) of mpvar, print_int:boolean)
    declarations
      sep = " | "
      col_length: array(1..getsize(header)) of integer
    end-declarations

    line_length := 0
    decimal_points := 6
    if(print_int) then
      decimal_points := 0
    end-if

    max_col_length := 12
    if(print_int) then
      max_col_length := 2
    end-if

    writeln
    PrintTitle(title)
    !! print table header
    forall(i in 1..getsize(header)) do
      space := maxlist(getsize(header(i)), max_col_length)
      write(strfmt(header(i), space), sep)
      line_length += space + getsize(sep)
      col_length(i) := space
    end-do
    writeln

    !! print line sep
    forall(i in 1..line_length) write("-")
    writeln

    !! print data
    forall(i in first_iter) do
      write(strfmt(row_prefix + i, col_length(1)), sep)
      col := 1
      forall(j in sec_iter) do
        col += 1
        write(strfmt(abs(getsol(objToPrint(i,j))), col_length(col), decimal_points), sep)
      end-do
      writeln
    end-do

    !! print endling line
    forall(i in 1..line_length) write("-")
    writeln
  end-procedure

  procedure WriteResultsToCSV(header:string, filename:string, row_prefix:string, first_iter:range, sec_iter:range, objToReadFrom:array(range, range) of mpvar, print_int:boolean)
    decimal_points := 6
    if(print_int) then
      decimal_points := 0
    end-if

    fopen(results_dir + filename, F_OUTPUT)
      writeln(header)
      forall(i in first_iter) do
  	    write(row_prefix, i, ",")
  	    forall(j in sec_iter) do
    	    write(strfmt(abs(getsol(objToReadFrom(i,j))), 0, decimal_points), ",")
    	  end-do
    	  writeln
  	  end-do
    fclose(F_OUTPUT)
  end-procedure

  procedure WriteResultsToCSV(header:string, filename:string, row_prefix:string, first_iter:range, sec_iter:set of string, objToReadFrom:array(range, set of string) of mpvar, print_int:boolean)
    decimal_points := 6
    if(print_int) then
      decimal_points := 0
    end-if

    fopen(results_dir + filename, F_OUTPUT)
      writeln(header)
      forall(i in first_iter) do
  	    write(row_prefix, i, ",")
  	    forall(j in sec_iter) do
    	    write(strfmt(abs(getsol(objToReadFrom(i,j))), 0, decimal_points), ",")
    	  end-do
    	  writeln
  	  end-do
    fclose(F_OUTPUT)
  end-procedure

  procedure WriteDataToCSV(header:string, filename:string, row_prefix:string, first_iter:range, sec_iter:range, objToReadFrom:array(range, range) of real)
    fopen(data_dir + filename, F_OUTPUT)
      writeln(header)
      forall(i in first_iter) do
  	    write(row_prefix, i, ",")
  	    forall(j in sec_iter) do
    	    write(objToReadFrom(i,j), ",")
    	  end-do
    	  writeln
  	  end-do
    fclose(F_OUTPUT)
  end-procedure

  procedure WriteDataToCSV(header:string, filename:string, row_prefix:string, first_iter:range, sec_iter:range, objToReadFrom:array(range, range) of integer)
    fopen(data_dir + filename, F_OUTPUT)
      writeln(header)
      forall(i in first_iter) do
  	    write(row_prefix, i, ",")
  	    forall(j in sec_iter) do
    	    write(objToReadFrom(i,j), ",")
    	  end-do
    	  writeln
  	  end-do
    fclose(F_OUTPUT)
  end-procedure
end-model
